// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1x'
    }
}

allprojects {
    repositories {
        google()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        jcenter()
    }
}

ext {
    compileSdkVersion = 28

    minSdkVersion = 8
    targetSdkVersion = 23
}

task aggregateJavadocs(type: Javadoc) {
    destinationDir = file("$buildDir/docs/javadoc")
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin(JavaPlugin)) {
            rootProject.tasks.aggregateJavadocs {
                source += javadoc.source
                classpath += files(javadoc.classpath)
            }
        }
        if (plugins.hasPlugin(com.android.build.gradle.LibraryPlugin)) {
            rootProject.tasks.aggregateJavadocs {
                source += files(android.sourceSets.main.java.srcDirs)
                classpath += files(project.files(android.getBootClasspath().join(File.pathSeparator)))
            }
        }
    }
}
